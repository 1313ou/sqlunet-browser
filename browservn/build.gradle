/*
 * Copyright (c) 2019. Bernard Bou <1313ou@gmail.com>.
 */

apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    namespace 'org.sqlunet.browser.vn'

    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        applicationId "org.sqlunet.browser.vn"
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['src/main/assets', 'src/debug/assets/']
        }
    }

    //assetPacks = [":dbvn_wn31_asset", ":dbvn_ewn_asset"]
    assetPacks = [":dbvn_ewn_asset"]

    signingConfigs {
        sqlunet {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.sqlunet
            versionNameSuffix 'signed'
        }
        debug {
            signingConfig signingConfigs.sqlunet
        }
    }
}

dependencies {

    implementation project(path: ':common')
    implementation project(path: ':expandableListFragment')
    implementation project(path: ':assetpacks')
    implementation project(path: ':nightmode')
    implementation project(path: ':download')
    implementation project(path: ':deploy')
    implementation project(path: ':others')
    implementation project(path: ':donate')
    implementation project(path: ':rate')
    implementation project(path: ':xNet')
    implementation project(path: ':wordNet')
    implementation project(path: ':verbNet')
    implementation project(path: ':propbank')

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.navigation:navigation-fragment:$navVersion"
    implementation "androidx.navigation:navigation-ui:$navVersion"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.preference:preference:$preferenceVersion"
    implementation "androidx.annotation:annotation:$annotationVersion"
    implementation "com.google.android.material:material:$materialVersion"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugaring"
    implementation "androidx.multidex:multidex:2.0.1"

    //local unit tests (JUnit 4 framework)
    testImplementation 'junit:junit:4.13.2'

    // instrumented tests
    // androidTestImplementation project(path: ':nightmode')
    androidTestImplementation project(path: ':common')
    androidTestImplementation project(path: ':nightmode')
    androidTestImplementation project(path: ':test')
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.annotation:annotation:$annotationVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    
    constraints {
    	implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.20") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
    	}
    	implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
    	}
    }
}
