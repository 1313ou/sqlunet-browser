/*
 * Copyright (c) 2019. Bernard Bou <1313ou@gmail.com>.
 */

plugins {
    id 'org.sqlunet.plugin.querybuilder' version '1.0.0'
    id 'com.android.library'
}

android {

    namespace 'org.sqlunet.framenet'

    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {

    implementation project(path: ':xNet')
    implementation project(path: ':treeView')

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.annotation:annotation:$annotationVersion"
    implementation "com.google.android.material:material:$materialVersion"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugaring"

    testImplementation project(path: ':test-sql')
    testImplementation 'junit:junit:4.13.2'
}

def protoSrcDir = new File(projectDir, "/src/proto")
def generatedSrcDir = new File(buildDir, "generated/source/queries")

android {

    sourceSets {

        main {
            java.srcDirs += generatedSrcDir
        }

        test {
            java.srcDirs += generatedSrcDir
        }
    }
}

tasks.register('dumpSourceSets') {
    doLast {
        android.sourceSets.each { srcSet ->
            println "[" + srcSet.name + "]"
            print "-->Source directories: " + srcSet.java.srcDirs + "\n"
            print "-->Resource directories: " + srcSet.resources.srcDirs + "\n"
            println ""
        }
    }
}

querybuilder_args {

    inDir = "${protoSrcDir}/framenet"
    factory = "Factory.java"
    variables = ["Names.properties", "NamesExtra.properties"]
    instantiates = ["SqLiteDialect.java"]

    outDir = "${generatedSrcDir}"
    v = "V"
    q = "Q"
    qv = "QV"
    qPackage = "org.sqlunet.framenet.provider"
    instantiateDest = "org.sqlunet.framenet.sql"
}

tasks.register('generateAll') {
    dependsOn('generateQV')
            .dependsOn('generateQ')
            .dependsOn('generateV')
            .dependsOn('instantiate')
}

preBuild {
    dependsOn('generateAll')
}
